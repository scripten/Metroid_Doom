ACTOR Skree : MonsterBase
{
  +SPAWNCEILING
  +NOGRAVITY
  +NOVERTICALMELEERANGE
  Health 5
  Height 29
  Radius 10
  Speed 8
  Scale 0.5
  MeleeRange 64
  Obituary "%o was startled by a Skree."
  DamageType "NoMonsterDamage"
  States
  {
  Spawn:
    SKRE A 0 NODELAY A_ChangeVelocity(0, 0, 500)
    SKRE AB 4 A_Look
    SKRE CD 4 A_Look
    SKRE EF 4 A_Look
    SKRE CG 4 A_Look
    loop
  See:
    SKRE ABCDEFCG 16 A_JumpIfTargetInsideMeleeRange("PreAttack")
    loop
  PreAttack:
    SKRE A 0 A_Scream
    SKRE A 0 A_Gravity
    SKRE A 0 A_ChangeFlag("THRUACTORS", 1)
    Goto Attack
  Attack:
    SKRE HI 3 A_JumpIf(z - floorz == 0, "Burrow")
    SKRE A 0 A_Chase
    loop
  Burrow:
    SKRE A 0 A_SpawnItemEx("SkreeChunker", 0, 0, 8, 0, 0, 0, 0, 0, 0)
    SKRE JKLMNO 3
    Stop
  Pain:
    SKRE A 2 Thing_SetTranslation(0, 7)
    SKRE A 1 Thing_SetTranslation(0, 0)
    Goto See
  Death:
    SKRE A 1 A_Scream
    SKRE A 0 A_SpawnItemEx("PickupSpawnerEnergy", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    SKRE A 0 A_NoBlocking
    SKRE A 0 A_SpawnDebris("Chunk")
    Stop
  Ice:
    TNT1 A 0 A_SpawnItemEx("SkreeIce", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    TNT1 A 0 A_PlaySound("misc/freeze")
    Stop
  }
}

ACTOR SkreeIce : IceBase
{
  Height 8
  Radius 12
  Scale 0.5
  States
  {
  Spawn:
    SKRE A 500
    SKRE A 1 A_Jump(256, "Death")
    Loop
  Death:
    SKRE A 0 A_SpawnItemEx("PickupSpawnerEnergy", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    SKRE A 0 A_FreezeDeathChunks
    Stop
  }
}

ACTOR SkreeChunker
{
  Radius 1
  Height 1
  +NOGRAVITY
  DamageType "NoMonsterDamage"
  States
  {
  Spawn:
    TNT1 AAAAA 0 A_SpawnItemEx("DamageChunk", random(-16, 16), random(-16, 16), random(0, 10))
    TNT1 A 1 A_JumpIf(random(0, 2) == 2, "Death")
    loop
  Death:
    TNT1 A 1
    Stop
  }
}