ACTOR Ridley : MonsterBase 10060
{
  +NOGRAVITY
  +FLOAT
  +BOSS
  +DONTRIP
  +JUMPDOWN
  +NOICEDEATH
  Height 400
  Radius 150
  Mass 0x7FFFFFFF
  Health 3500
  DeathSound "RIDLSCRM"
  PainChance 50
  Gravity 0.001
  Speed 40
  Scale 3.0
  MeleeRange 400
  Obituary "%o was devoured by Ridley."
  DamageFactor "Beam", 0.0
  DamageFactor "Electric", 0.0
  DamageFactor "ChargePlasma", 2.0
  DamageFactor "SpeedBooster", 0.0
  DamageFactor "ScrewAttack", 0.0
  DamageFactor "NoMonsterDamage", 0.0
  DamageType "NoMonsterDamage"
  States
  {
  Spawn:
    RIDL ABCDE 6 A_Look
    RIDL DCB 5
    Loop
  See:
    Goto Roar
  Pain:
    RIDL A 0 A_JumpIf(random(0,2) == 2, "FireBall")
    RIDL A 0 A_JumpIfHealthLower(1000, "LowHealth")
    RIDL A 0 A_JumpIfHealthLower(2000, "MidHealth")
    Goto Roar
  LowHealth:
    RIDL A 0 Thing_SetTranslation(0, 9)
    Goto Roar
  MidHealth:
    RIDL A 0 Thing_SetTranslation(0, 8)
    Goto Roar
  See2:
    RIDL A 0 A_PlaySound("RIDLFLY")
    RIDL ABCD 5 A_Chase
    RIDL E 3 A_SentinelBob
    RIDL A 0 A_JumpIfTargetInsideMeleeRange("Charge")
    RIDL A 0 A_JumpIfCloser(256, "SprayFire")
    RIDL A 0 A_JumpIf(random(0,2) == 2, "Fireball")
    RIDL A 0 A_JumpIf(random(0,5) == 5, "Roar")
    RIDL DCB 5 A_Wander
    Loop
  Roar:
    RIDL A 1 A_SentinelBob
    RIDL F 3 A_Scream
    RIDL G 18
    RIDL FA 7
    RIDL A 1 A_SentinelBob
    Goto See2
  Fireball:
    RIDL A 0 A_JumpIf(random(0,2) == 2, "SpreadFireball")
    RIDL F 3 A_Scream
    RIDL G 0 A_FaceTarget
    RIDL GGGGGGGGGG 5 A_CustomMissile("RidleyFireball", 190, 50)
    RIDL FA 6
    RIDL A 1 A_SentinelBob
    Goto See2
  SpreadFireball:
    RIDL A 0 A_JumpIf(random(0,2) == 2, "SprayFire")
    RIDL F 3 A_Scream
    RIDL G 0 A_FaceTarget
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, -30, 0, -20)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, -25, 0, -15)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, -15, 0, -10)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, -5)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, 10, 0, 5)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, 20, 0, 10)
    RIDL G 8 A_CustomMissile("RidleyFireball", 190, 50, 30, 0, 15)
    RIDL FA 6
    RIDL A 1 A_SentinelBob
    Goto See2
  SprayFire:
    RIDL F 3 A_Scream
    RIDL G 0 A_FaceTarget
    RIDL GGGGGG 5 A_SpawnItemEx("RidleyChunker", 180, 50, 170)
    RIDL FA 6
    RIDL A 1 A_SentinelBob
    Goto See2
  Charge:
    RIDL A 0 A_JumpIf(random(0,2) == 2, "SprayFire")
    RIDL A 0 A_PlaySound("RIDLFLY")
    RIDL ABCDE 3 A_Chase
    RIDL FG 4 A_FaceTarget
    RIDL F 1 A_CustomMeleeAttack((120), "RDLBITE1", "RDLBITE2")
    RIDL ABCDE 5 A_SentinelBob
    Goto See2
  Death:
    RIDL A 0 A_Gravity
    RIDL FGFGFG 16 A_Scream
    RIDL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PickupSpawner", random(-300, 300), random(-300, 300), random(-300, 300), 0, 0, 0, 0, 0, 0, tid)
    RIDL A 0 A_NoBlocking
    RIDL AAAAA 0 A_SpawnDebris("Chunk")
    Stop
  }
}

ACTOR RidleyFireball
{
  height 24
  radius 24
  damage (120)
  speed 32
  Scale 2
  PROJECTILE
  +RANDOMIZE
  -NOBLOCKMAP
  +SOLID
  Obituary "%o was roasted by Ridley's flames."
  DeathSound "vile/firecrkl"
  SeeSound "vile/firestrt"
  DamageType "NoMonsterDamage"
  states
  {
  Spawn:
    RDFR ABCD 1
    loop
  Death:
    BAL2 DE 4
    TNT1 A 0 A_SpawnItemEx("RidleyChunker", 0, 0, 8)
    stop
  }
}

ACTOR RidleyChunker
{
  Radius 1
  Height 1
  +NOGRAVITY
  States
  {
  Spawn:
    TNT1 AAA 0 A_SpawnItemEx("RidleySpreadFire", random(-32, 16), random(-32, 16), random(0, 10))
    TNT1 A 1 A_JumpIf(random(0, 2) == 2, "Death")
    loop
  Death:
    TNT1 A 1
    Stop
  }
}

ACTOR RidleySpreadFire
{
  PROJECTILE
  -NOGRAVITY
  radius 8
  height 8
  Obituary "%o was roasted by Ridley's flames."
  DamageType "NoMonsterDamage"
  SeeSound "vile/firestrt"
  Damage (80)
  DamageType "NoMonsterDamage"
  states
  {
  Spawn:
	   RDFR A 0 NoDelay A_ChangeVelocity(random(-8.0, 8.0), random(-8.0, 8.0), 4.0)
    Goto Spawn2
  Spawn2:
    RDFR ABCD 1
    loop
	 XDeath:
	   TNT1 A 0
	   Stop
  Death:
	   TNT1 A 0 A_SpawnItemEx("RidleyFire", 0, 0, 8)
	   Stop
  }
}

ACTOR RidleyFire
{
  Health 0x7FFFFFFF
  radius 16
  height 1
  scale 0.5
  +THRUACTORS
  Obituary "%o was roasted by Ridley's flames."
  SeeSound "vile/firestrt"
  DamageType "NoMonsterDamage"
  states
  {
  Spawn:
    RDFL A 0 A_JumpIf(random(0, 5) == 5, "Death")
    RDFL A 0 A_Stop
    RDFL ABCD 8 A_Explode(80, 32, False, False, True)
    Loop
  Death:
    RDFL ABCDA 8 A_FadeOut(0.2)
    Stop
  }
}