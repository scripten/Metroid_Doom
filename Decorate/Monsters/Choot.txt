ACTOR Choot : MonsterBase
{
  Health 14
  Height 12
  Radius 10
  Speed 8
  Scale 0.8
  Gravity 0.1
  Obituary "%o got digested by a Choot."
  DamageType "NoMonsterDamage"
  States
  {
  Spawn:
	  CHOO A 1 A_Look
    loop
	 See:
	  CHOO A 5 A_Jump(50, "Jump")
	  loop
  Jump:
    CHOO B 3 A_ChangeVelocity(0, 0, random(3,4))
    CHOO C 3
  CheckVel:
    CHOO C 1 A_JumpIf(velz <= 2, "Attack")
    loop
  Attack:
    CHOO B 1 A_FaceTarget
    CHOO C 0 A_CustomMissile("ChootGlob", 0)
    goto FloatDown
  FloatDown:
    CHOO D 1 A_JumpIf(velz == 0, "Spawn")
    loop
  Pain:
    CHOO A 1 Thing_SetTranslation(0, 7)
    CHOO A 1 Thing_SetTranslation(0, 0)
    Goto FloatDown
  Death:
    CHOO A 1 A_Scream
    CHOO A 0 A_SpawnItemEx("PickupSpawnerEnergy", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    CHOO A 0 A_NoBlocking
    CHOO A 0 A_SpawnDebris("Chunk")
    Stop
  Ice:
    TNT1 A 0 A_SpawnItemEx("ChootIce", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    TNT1 A 0 A_PlaySound("misc/freeze")
    Stop
  }
}

ACTOR ChootIce : IceBase
{
  Height 12
  Radius 12
  Scale 0.8
  States
  {
  Spawn:
    CHOO A 500
    CHOO A 1 A_Jump(256, "Death")
    Loop
  Death:
    CHOO A 0 A_SpawnItemEx("PickupSpawnerEnergy", 0, 0, 0, 0, 0, 0, 0, 0, 0, tid)
    CHOO A 0 A_FreezeDeathChunks
    Stop
  }
}

ACTOR ChootGlob
{
  height 2
  radius 2
  damage (12)
  speed 8
  Scale 0.2
  RenderStyle "Translucent"
  Alpha 0.3
  PROJECTILE
  +RANDOMIZE
  DamageType "NoMonsterDamage"
  states
  {
  Spawn:
    CHGB ABC 1
    loop
  Death:
    TNT1 A 1
    stop
  }
}